INICIO -> ACOES
ACOES -> ACOES_A ACOES_B
ACOES -> ACOES_B
ACOES -> ''
ACOES_A -> DECLARACAO
ACOES_A -> ATRIBUICAO
ACOES_A -> ESTRUTURA
ACOES_A -> LEIA
ACOES_A -> ESCREVA
ACOES_B -> \n ACOES
ACOES_INTERNAS -> ACOES_INTERNAS_A ACOES_INTERNAS_B
ACOES_INTERNAS -> ACOES_INTERNAS_B
ACOES_INTERNAS -> ''
ACOES_INTERNAS_A -> ATRIBUICAO
ACOES_INTERNAS_A -> ESTRUTURA
ACOES_INTERNAS_A -> LEIA
ACOES_INTERNAS_A -> ESCREVA
ACOES_INTERNAS_B -> \n ACOES_INTERNAS
TIPO_DE_DADO -> int
TIPO_DE_DADO -> real
TIPO_DE_DADO -> caractere
TIPO_DE_DADO -> bool
DECLARACAO -> var TIPO_DE_DADO variavel DECLARACAO_A
DECLARACAO_A -> , variavel DECLARACAO_A
DECLARACAO_A -> ''
EXPRESSAO -> EXPRESSAO ou EXPRESSAO_A
EXPRESSAO -> EXPRESSAO_A
EXPRESSAO_A -> EXPRESSAO_A e EXPRESSAO_B
EXPRESSAO_A -> EXPRESSAO_B
EXPRESSAO_B -> EXPRESSAO_B xou EXPRESSAO_C
EXPRESSAO_B -> EXPRESSAO_C
EXPRESSAO_C -> nao EXPRESSAO_C
EXPRESSAO_C -> EXPRESSAO_D
EXPRESSAO_D -> EXPRESSAO_D COMPARADOR EXPRESSAO_E
EXPRESSAO_D -> EXPRESSAO_E
COMPARADOR -> <=
COMPARADOR -> =
COMPARADOR -> >=
COMPARADOR -> <
COMPARADOR -> >
COMPARADOR -> <>
EXPRESSAO_E -> EXPRESSAO_E OPERADORES_EXPRESSAO_E EXPRESSAO_F
EXPRESSAO_E -> EXPRESSAO_F
OPERADORES_EXPRESSAO_E -> +
OPERADORES_EXPRESSAO_E -> -
EXPRESSAO_F -> EXPRESSAO_F OPERADORES_EXPRESSAO_F EXPRESSAO_G
EXPRESSAO_F -> EXPRESSAO_G
OPERADORES_EXPRESSAO_F -> *
OPERADORES_EXPRESSAO_F -> /
OPERADORES_EXPRESSAO_F -> %
OPERADORES_EXPRESSAO_F -> //
EXPRESSAO_G -> OPERADORES_EXPRESSAO_G EXPRESSAO_G
EXPRESSAO_G -> EXPRESSAO_H
OPERADORES_EXPRESSAO_G -> +
OPERADORES_EXPRESSAO_G -> -
EXPRESSAO_H -> EXPRESSAO_H ^ EXPRESSAO_I
EXPRESSAO_H -> EXPRESSAO_I
EXPRESSAO_I -> ( EXPRESSAO )
EXPRESSAO_I -> numero
EXPRESSAO_I -> numero_real
EXPRESSAO_I -> variavel
EXPRESSAO_I -> char
EXPRESSAO_I -> verdadeiro
EXPRESSAO_I -> falso
ATRIBUICAO -> variavel ATRIBUIDOR ATRIBUICAO_A
ATRIBUICAO_A -> ATRIBUICAO
ATRIBUICAO_A -> EXPRESSAO
ATRIBUIDOR -> <-
ATRIBUIDOR -> +<-
ATRIBUIDOR -> -<-
ATRIBUIDOR -> *<-
ATRIBUIDOR -> /<-
CONDICAO -> EXPRESSAO
CONDICAO -> ATRIBUICAO
\N* -> \n \N*
\N* -> ''
SE -> se ( CONDICAO ) \N* { \n ACOES_INTERNAS } SENAO_SE
SENAO_SE -> senao_se ( CONDICAO ) \N* { \n ACOES_INTERNAS } SENAO_SE
SENAO_SE -> SENAO
SENAO -> senao \N* { \n ACOES_INTERNAS }
SENAO -> ''
ENQUANTO -> enquanto ( CONDICAO ) \N* { \n ACOES_INTERNAS }
REPITA -> repita \N* { \n ACOES_INTERNAS } enquanto ( CONDICAO )
PARA -> para ( ATRIBUICAO ; CONDICAO ; ATRIBUICAO ) \N* { \n ACOES_INTERNAS }
ESTRUTURA -> SE
ESTRUTURA -> ENQUANTO
ESTRUTURA -> REPITA
ESTRUTURA -> PARA
LEIA -> leia ( variavel )
ESCREVA -> escreva ( EXPRESSAO ESCREVA_A )
ESCREVA_A -> , EXPRESSAO ESCREVA_A
ESCREVA_A -> ''
