<inicio>::=<acoes>

<acoes>::=<acoes_A> <acoes_B>|<acoes_B>|ε
<acoes_A>::=<declaracao>|<atribuicao>|<estrutura>|<leia>|<escreva>
<acoes_B>::=\n <acoes>

<acoes_internas>::=<acoes_internas_A> <acoes_internas_B>|<acoes_internas_B>|ε
<acoes_internas_A>::=<atribuicao>|<estrutura>|<leia>|<escreva>
<acoes_internas_B>::=\n <acoes_internas>

<tipo_de_dado>::=int|real|caractere|bool

<declaracao>::=var <tipo_de_dado> variavel <declaracao_A>
<declaracao_A>::=, variavel <declaracao_A>|ε

<expressao>::=<expressao> ou <expressao_A>|<expressao_A>
<expressao_A>::=<expressao_A> e <expressao_B>|<expressao_B>
<expressao_B>::=<expressao_B> xou <expressao_C>|<expressao_C>
<expressao_C>::=nao <expressao_C>|<expressao_D>
<expressao_D>::=<expressao_D> <comparador> <expressao_E>|<expressao_E>
<comparador>::=<=|=|>=|<|>|<>
<expressao_E>::=<expressao_E> <operadores_expressao_E> <expressao_F>|<expressao_F>
<operadores_expressao_E>::=+|-
<expressao_F>::=<expressao_F> <operadores_expressao_F> <expressao_G>|<expressao_G>
<operadores_expressao_F>::=*|/|%|//
<expressao_G>::=<operadores_expressao_G> <expressao_G>|<expressao_H>
<operadores_expressao_G>::=+|-
<expressao_H>::=<expressao_H> ^ <expressao_I>|<expressao_I>
<expressao_I>::=( <expressao> )|numero|numero_real|variavel|char|verdadeiro|falso

<atribuicao>::=variavel <atribuidor> <atribuicao_A>
<atribuicao_A>::=<atribuicao>|<expressao>
<atribuidor>::=<-|+<-|-<-|*<-|/<-

<condicao>::=<expressao>|<atribuicao>

<\n*>::=\n <\n*>|ε

<se>::=se ( <condicao> ) <\n*> { \n <acoes_internas> } <senao_se>
<senao_se>::=senao_se ( <condicao> ) <\n*> { \n <acoes_internas> } <senao_se>|<senao>
<senao>::=senao <\n*> { \n <acoes_internas> }|ε

<enquanto>::=enquanto ( <condicao> ) <\n*> { \n <acoes_internas> }

<repita>::=repita <\n*> { \n <acoes_internas> } enquanto ( <condicao> )

<para>::=para ( <atribuicao> ; <condicao> ; <atribuicao> ) <\n*> { \n <acoes_internas> }

<estrutura>::=<se>|<enquanto>|<repita>|<para>

<leia>::=leia ( variavel )

<escreva>::=escreva ( <expressao> <escreva_A> )
<escreva_A>::=, <expressao> <escreva_A>|ε
